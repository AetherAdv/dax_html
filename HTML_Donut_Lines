HTML_Donut_Lines = 

VAR pct = Parameter1[Parameter Value]
VAR pctText = FORMAT(pct * 100, "0") & "%"
VAR sizeWidth = 300
VAR sizeHeight = 300

VAR baseColor = "#292929"
VAR textColor = "#f5f5f5"
VAR subtitleColor = "#bbb"

-- Donut line specific vars
VAR numLines = 30
VAR linesToShow = ROUND(pct * numLines, 0)

VAR centerX = sizeWidth / 2
VAR centerY = sizeHeight / 2
VAR orbitRadius = MIN(sizeWidth, sizeHeight) * 0.4
VAR lineLength = MIN(sizeWidth, sizeHeight) * 0.07
VAR lineThickness = 6
VAR centerCircleRadius = MIN(sizeWidth, sizeHeight) * 0.067
VAR fontSize = MIN(sizeWidth, sizeHeight) * 0.16

VAR fillColor =
    SWITCH(
        TRUE(),
        pct < 0.2, "#E59EFB",
        pct > 0.8, "#F8FAB7",
        "#6bfad8"
    )

VAR lines = 
    CONCATENATEX (
        GENERATESERIES(0, numLines - 1, 1),
        VAR i = [Value]
        VAR angle = i * (360 / numLines)
        VAR isActive = i < linesToShow
        VAR lineColor = IF(isActive, fillColor, "#333")
        VAR animation = IF(isActive, "fadeLine 2s ease-in-out infinite", "none")
        VAR delay = FORMAT(i * (2 / numLines), "0.00") & "s"
        RETURN
            "<line x1='" & centerX & "' y1='" & (centerY - orbitRadius + lineLength) & "' x2='" & centerX & "' y2='" & (centerY - orbitRadius) & "' stroke='" & lineColor & "' stroke-width='" & lineThickness & "'
            style='transform: rotate(" & angle & "deg); transform-origin: " & centerX & "px " & centerY & "px; animation: " & animation & "; animation-delay: " & delay & ";' />"
    )

VAR svg = "
<svg xmlns='http://www.w3.org/2000/svg' width='" & sizeWidth & "' height='" & sizeHeight & "' viewBox='0 0 " & sizeWidth & " " & sizeHeight & "'>
  <style>
    .card-base {
      fill: " & baseColor & ";
    }
    @keyframes fadeLine {
      0%, 100% { opacity: 1; }
      50% { opacity: 0.3; }
    }
    .label {
      font-family: Segoe UI, sans-serif;
      font-size: " & fontSize & "px;
      font-weight: 900;
      fill: " & fillColor & ";
      text-anchor: middle;
      dominant-baseline: middle;
    }
    .center-circle {
      fill: #222;
    }
  </style>

  <!-- Background -->
  <!-- <rect class='card-base' width='" & sizeWidth & "' height='" & sizeHeight & "' rx='20' ry='20'/> -->

  <!-- Rotating thick lines -->
  " & lines & "

  <!-- Center circle -->
  <circle cx='" & centerX & "' cy='" & centerY & "' r='" & centerCircleRadius & "' class='center-circle'/>

  <!-- Percentage label -->
  <text x='" & centerX & "' y='" & centerY & "' class='label'>" & pctText & "</text>
</svg>
"

RETURN "data:image/svg+xml;utf8," & svg
