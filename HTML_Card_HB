HTML_Card_HB = 

VAR pct = Parameter1[Parameter Value]
VAR pctText = FORMAT(pct * 100, "0") & "%"
VAR sizeWidth = 320
VAR sizeHeight = 180

-- Colors
VAR baseColor = "#292929"
VAR textColor = "#f5f5f5"

VAR fillColor = 
    SWITCH(
        TRUE(),
        pct < 0.2, "#E59EFB",
        pct > 0.8, "#F8FAB7",
        "#6bfad8"
    )

-- Dynamic heartbeat height based on pct
VAR high = 90 - (pct * 40)
VAR low = 90 + (pct * 40)

-- Heartbeat path
VAR heartbeatPath = 
    "M 20 90 " &
    "L 60 90 " &
    "L 70 " & high &
    " L 80 " & low &
    " L 90 90 " &
    "L 130 90 " &
    "L 140 " & high &
    " L 150 " & low &
    " L 160 90 " &
    "L 200 90 " &
    "L 210 " & high &
    " L 220 " & low &
    " L 230 90 " &
    "L 270 90 " &
    "L 280 " & high &
    " L 290 " & low &
    " L 300 90"

-- SVG output
VAR svg = "
<svg xmlns='http://www.w3.org/2000/svg' width='" & sizeWidth & "' height='" & sizeHeight & "' viewBox='0 0 " & sizeWidth & " " & sizeHeight & "'>
  <style>
    .card-base {
      fill: " & baseColor & ";
    }
    .heartbeat {
      fill: none;
      stroke: " & fillColor & ";
      stroke-width: 4;
      stroke-linejoin: round;
      stroke-linecap: round;
      stroke-dasharray: 600;
      stroke-dashoffset: 600;
      animation: pulseLine 1.5s linear infinite;
    }
    .title {
      font-family: Segoe UI, sans-serif;
      font-size: 32px;
      font-weight: bold;
      fill: " & textColor & ";
    }
    @keyframes pulseLine {
      0% { stroke-dashoffset: 600; }
      100% { stroke-dashoffset: 0; }
    }
  </style>

  <!-- Background -->
  <rect class='card-base' width='" & sizeWidth & "' height='" & sizeHeight & "' rx='20' ry='20'/>

  <!-- Heartbeat path -->
  <path d='" & heartbeatPath & "' class='heartbeat'/>

  <!-- Percentage text -->
  <text x='30' y='40' class='title'>" & pctText & "</text>
</svg>
"

RETURN "data:image/svg+xml;utf8," & svg
